buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['RNTangemSdk_' + name]
  }

  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
      maven { url 'https://jitpack.io' }
    }

    dependencies {
      classpath "com.android.tools.build:gradle:7.3.1"
      // noinspection DifferentKotlinGradleVersion
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    }
  } else {
    repositories {
      mavenCentral()
    }

    dependencies {
      // noinspection DifferentKotlinGradleVersion
      classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}")
    }
  }
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['RNTangemSdk_' + name]).toInteger()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
  buildToolsVersion getExtOrDefault('buildToolsVersion')
  defaultConfig {
    minSdkVersion getExtOrIntegerDefault('minSdkVersion')
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
}

rootProject.allprojects {
  repositories {
    maven { url 'https://jitpack.io' }
  }
}

dependencies {
  // noinspection GradleDynamicVersion
  implementation 'com.facebook.react:react-android:+'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:${getExtOrDefault('kotlinVersion')}"

  // tangem sdk dependencies
  implementation "com.github.tangem.tangem-sdk-android:android:${getExtOrDefault('tangemSdkVersion')}"
  implementation "com.github.tangem.tangem-sdk-android:core:${getExtOrDefault('tangemSdkVersion')}"
}
